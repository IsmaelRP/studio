apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "es.iessaldillo.pedrojoya.pr183"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "TITULO", "\"" + TITULO + "\"")
        resValue("string", "texto", "\"" + TEXTO + "\"")
        buildConfigField("String", "SECRETO1", "\"" + getSecreto() + "\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "NUEVOTEXTO", "\"" +
                    project.property('NUEVOTEXTO') + "\""
        }
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "SECRETO2", "\"" +
                    getSecreto() + "\""
        }
    }

}

// Obtiene del archivo secret.properties el valor de la propiedad correspodiente
def getSecreto() {
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('secret.properties')))
    return props['SECRETO']
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportlibraryVersion"
    implementation "com.google.firebase:firebase-messaging:$rootProject.ext.firebasemessagingVersion"
    implementation "com.squareup.okhttp3:okhttp:$rootProject.ext.okhttp3Version"

    testImplementation "junit:junit:$rootProject.ext.junitVersion"
}

apply plugin: 'com.google.gms.google-services'

